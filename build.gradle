plugins {
    id 'java'
    id 'maven'
    id 'com.github.hierynomus.license' version '0.12.1'
    id 'net.ellune.blossom' version '1.0.1'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group 'me.flibio'
version '1.3.0'

defaultTasks 'licenseFormat', 'clean', 'build'

repositories {
    mavenCentral()
    jcenter()
    maven {
        name 'Sponge API repo'
        url 'http://repo.spongepowered.org/maven'
    }
    maven {
        name = 'minecrell'
        url = 'http://repo.minecrell.net/releases'
    }
    maven { url "https://jitpack.io" }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    compile 'org.spongepowered:spongeapi:3.1.0-SNAPSHOT'
    shade 'com.github.flibio:updatifier:v1.3.0'
    shade 'net.minecrell.mcstats:statslite-sponge:0.2.1'
}

license {

    // The project properties to use
    ext.name = project.name
    ext.owner = project.owner
    ext.inceptionYear = project.inceptionYear
    ext.currentYear = project.currentYear

    // The file that contains the license header
    header file('HEADER.txt')

    // The source sets to apply the license to
    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

jar {
    configurations.shade.copyRecursive().setTransitive(false).each { artifact ->
        from (zipTree(artifact))
    }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.11'
}

blossom {
    replaceToken '@project.version@', project.version, 'src/main/java/me/flibio/jobslite/PluginInfo.java'
}
